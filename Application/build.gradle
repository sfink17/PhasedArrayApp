apply plugin: 'com.android.model.application'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

model {
    repositories {
        libs(PrebuiltLibraries) {
            superpowered { // this is where you declare the "superpowered" static library
                headers.srcDir "src/main/jniLibs/superpowered"
                binaries.withType(StaticLibraryBinary) { // attaching library files to each platform
                    def platformName = targetPlatform.getName()
                    if (platformName == "armeabi-v7a") {
                        staticLibraryFile = file("src/main/jniLibs/superpowered/libSuperpoweredAndroidARM.a")
                    } else if (platformName == "arm64-v8a") {
                        staticLibraryFile = file("src/main/jniLibs/superpowered/libSuperpoweredAndroidARM64.a")
                    } else if (platformName == "x86") {
                        staticLibraryFile = file("src/main/jniLibs/superpowered/libSuperpoweredAndroidX86.a")
                    } else if (platformName == "x86_64") {
                        staticLibraryFile = file("src/main/jniLibs/superpowered/libSuperpoweredAndroidX86_64.a")
                    }
                }
            }
        }
    }

    android { // main settings for your application
        compileSdkVersion = 23
        buildToolsVersion = "23.0.3"

        defaultConfig {
            applicationId "com.example.android.bluetoothchat"
            minSdkVersion.apiLevel = 16 // more than 95% of all active Android devices
            targetSdkVersion.apiLevel = 23
            versionCode 1
            versionName "1.0"
        }

        ndk {
            moduleName = "jniNativeAudio"
            platformVersion = 16
            stl = "c++_static"
            toolchain = 'clang'
            CFlags.addAll(["-O3", "-fsigned-char"]) // full optimization, char data type is signed
            cppFlags.addAll(["-fno-exceptions", "-fsigned-char", "-I${file("src/main/jniLibs/superpowered")}".toString()])
            ldLibs.addAll(["log", "android", "OpenSLES"])
            // load these libraries: log, android, OpenSL ES (for audio)
            abiFilters.addAll(["armeabi-v7a", "arm64-v8a", "x86", "x86_64"])
            // these platforms cover 99% percent of all Android devices
        }

        sources {
            main {
                jni {
                    source {
                        srcDir "jni"
                        srcDir "src/main/jniLibs/superpowered/AndroidIO"
                    }
                    dependencies {
                        library "superpowered" linkage "static"
                        // this is where you attach the "superpowered" static library to your app
                    }
                }
            }
        }
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.3.0'
}
